// Logical ports that correspond to VIF connections
view VIFPort( id : lport_id_t )
VIFPort(id) :- LogicalSwitchPort(id,_,LPortVIF{_,_,_},_,_,_,_,_,_)

// Non-VIF ports
view VSwitchPort( id            : lport_id_t
                , lswitch       : lswitch_id_t
                , ptype         : lport_type_t
                , name          : string
                , switch        : chassis_id_t
                , portnum       : bit<16>
                , enabled       : bool
                , dhcp4_options : opt_dhcp4_options_id_t
                , dhcp6_options : opt_dhcp6_options_id_t
                , primary key (id)
                , foreign key (switch) references Chassis(id)
                , unique (switch, portnum) )

VSwitchPort(id, lswitch, ptype, name, switch, portnum, enabled, dhcp4, dhcp6) :-
           LogicalSwitchPort(id, lswitch, ptype, name, switch, portnum, enabled, dhcp4, dhcp6),
           not VIFPort(id)

// Port security
typedef port_sec_type_t = PortSecNone
                        | PortSecMAC
                        | PortSecIP

view PortSecurityEnabled( lport : lport_id_t )
PortSecurityEnabled(lport) :- PortSecurityMAC(lport, _)

view PortIPSecurityEnabled( lport : lport_id_t )
PortIPSecurityEnabled(lport) :- PortSecurityMAC(lport, mac), PortSecurityIP(lport, mac, _)

view PortSecurityType ( lport : lport_id_t
                      , stype : port_sec_type_t)

PortSecurityType(lport, PortSecNone) :- LogicalSwitchPort(lport, _, _, _, _, _, _, _, _), not PortSecurityEnabled(lport)
PortSecurityType(lport, PortSecMAC) :- PortSecurityEnabled(lport), not PortIPSecurityEnabled(lport)
PortSecurityType(lport, PortSecIP) :- PortIPSecurityEnabled(lport)

view PortSecurityIP4Match( lport  : lport_id_t
                         , mac    : mac_addr_t
                         , subnet : ip4_subnet_t )

PortSecurityIP4Match(lport, mac, IP4Subnet{0,0}) :- PortSecurityMAC(lport, mac), not PortIPSecurityEnabled(lport)
PortSecurityIP4Match(lport, mac, subnet)         :- PortSecurityMAC(lport, mac), PortSecurityIP(lport, mac, IPSubnet4{subnet})

view PortSecurityIP6Match( lport  : lport_id_t
                         , mac    : mac_addr_t
                         , subnet : ip6_subnet_t )

PortSecurityIP6Match(lport, mac, IP6Subnet{0,0}) :- PortSecurityMAC(lport, mac), not PortIPSecurityEnabled(lport)
PortSecurityIP6Match(lport, mac, subnet)         :- PortSecurityMAC(lport, mac), PortSecurityIP(lport, mac, IPSubnet6{subnet})

// ACLs
view LPortStatefulACL(lport : lport_id_t)

LPortStatefulACL(lport) :- LogicalSwitchPort(lport, lswitch, _, _, _, _, _, _, _), ACL(lswitch, _, _, _, ACLAllowRelated)
LPortStatefulACL(lport) :- LogicalSwitchPort(lport, lswitch, _, _, _, _, _, _, _), ACL(lswitch, _, _, _, ACLAllow)

// Load balancing
view LPortLBVIP( lport : lport_id_t
               , vip   : ip4_addr_t)

LPortLBVIP(lport, vip) :- LogicalSwitchPort(lport, lswitch, _, _, _, _, _, _, _), LBSwitch(lb, lswitch), LBVIP(lb, IP4AddrPort{vip, _})
